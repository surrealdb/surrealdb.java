plugins {
    id 'java'
    id 'jacoco'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'com.surrealdb'
version '0.3.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

sourceSets {
    integrationTest {
        java
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    integrationTestImplementation files("build/libs/surrealdb-0.3.0-SNAPSHOT.jar")
}

jacoco {
    toolVersion = "0.8.12"
}

test {
    useJUnitPlatform()
    systemProperty 'java.library.path', file('target/debug').absolutePath
    testLogging {
        events "passed"
    }
}

jacocoTestReport {
    dependsOn test
    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.5
            }
        }
    }
}

javadoc {
    destinationDir = file("build/reports/javadoc")
}

tasks.register('javadocJar', Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

tasks.register('createCombinedReport') {
    dependsOn jacocoTestReport
    dependsOn javadoc

    doLast {
        def indexFile = file("build/reports/index.html")
        indexFile.text = """
            <!DOCTYPE html>
            <html>
            <head>
                <title>Combined Test, Coverage, and Javadoc Report</title>
            </head>
            <body>
                <h1>Combined Test, Coverage, and Javadoc Report</h1>
                <ul>
                    <li><a href="./tests/test/index.html">Test Report</a></li>
                    <li><a href="./jacoco/test/html/index.html">JaCoCo Coverage Report</a></li>
                    <li><a href="./javadoc/index.html">Javadoc</a></li>
                </ul>
            </body>
            </html>
        """
    }
}

tasks.register('integrationTest', Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    testLogging {
        showStandardStreams = true
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat = 'full'
        showCauses = true
        showStackTraces = true
    }
}

project.afterEvaluate {
    def key = System.getenv('SIGNING_KEY')
    def _pass = System.getenv('SIGNING_KEY_PASS')

    signing {
        useInMemoryPgpKeys(key, _pass)
        sign publishing.publications.mavenJava
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/surrealdb/surrealdb.java")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
//        maven {
//            name = "OSSRH"
//            def releasesRepoUrl = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
//            def snapshotsRepoUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
//            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
//            credentials {
//                username = System.getenv("MAVEN_USERNAME")
//                password = System.getenv("MAVEN_PASSWORD")
//            }
//        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.rootProject.name
            version = project.version

            artifact(javadocJar)
            artifact(sourcesJar)
            artifact("build/libs/${project.rootProject.name}-${project.version}.jar")


            pom {
                name = 'SurrealDB Driver'
                packaging = 'jar'
                description = 'The driver for accessing a SurrealDB instance.'
                url = 'https://surrealdb.com/docs/integration/libraries/java'

                scm {
                    connection = 'scm:git:git@github.com:surrealdb/surrealdb.java.git'
                    url = 'https://github.com/surrealdb/surrealdb.java'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'tobiemh'
                        name = 'Tobie Morgan Hitchcock'
                        email = 'tobie@surrealdb.com'
                    }
                    developer {
                        id = 'phughk'
                        name = 'Hugh Kaznowski'
                        email = 'hugh@surrealdb.com'
                    }
                    developer {
                        id = 'emmanuel-keller'
                        name = 'Emmanuel Keller'
                        email = 'emmanuel.keller@surrealdb.com'
                    }
                }
            }
        }
    }
}